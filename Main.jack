class Main {
    static int shipX, shipY, lives;
    
    function void main() {
        var boolean exit;
        var char key;

        let exit = false;
        let shipX = 250;
        let shipY = 220;
        let lives = 3;

        do Screen.clearScreen();
        do Alien.init();
        do Nave.init();

        while (~exit) {
            do Output.moveCursor(0,0);
            do Output.printString("LIVES: ");
            do Output.printInt(lives);

            do Nave.drawShip(shipX, shipY);

            let key = Keyboard.keyPressed();
            if (key = 130) {
                if (shipX > 6) {
                    let shipX = shipX - 5;
                }
            }
            if (key = 132) {
                if (shipX < 490) {
                    let shipX = shipX + 5;
                }
            }
            if (key = 32) {
                do Nave.fire(shipX + 10, shipY);
            }
            if (key = 140) {
                let exit = true;
            }

            do Alien.moveAll();
            do Nave.updateBullets();
            do Alien.fireBulletFromRandom();
            do Alien.updateAlienBullets();

            if (lives < 1) {
                do Screen.clearScreen();
                do Output.moveCursor(10,27);
                do Output.printString("GAME OVER");
                do Sys.wait(3000);
                do Sys.halt();
            }

            do Sys.wait(50);
            do Screen.clearScreen();
        }
        return;
    }

    function int getShipX() {
        return shipX;
    }

    function int getShipY() {
        return shipY;
    }

    function void loseLife() {
        let lives = lives - 1;
        return;
    }
}